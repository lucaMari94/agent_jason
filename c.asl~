/* Initial beliefs and rules */

all_proposals_received(CNPId)
  :- nb_participants(CNPId,NP) &                 // number of participants
     .count(propose(CNPId,_)[source(_)], NO) &   // number of proposes received
     .count(refuse(CNPId)[source(_)], NR) &      // number of refusals received
     NP = NO + NR.


/* Initial goals */

!startCNP(1,fix(computer)).
!startCNP(2,banana).

!register.
+!register <- .df_register(auctioneer).

/* Plans */

// start the CNP
+!startCNP(Id,Task)
   <- .print("Waiting participants for task ",Task,"...");
      .wait(2000);  // wait participants introduction
      +cnp_state(Id,propose);   // remember the state of the CNP annotazione: fase di propose
      .df_search("participant",LP);
      .print("Sending CFP to ",LP);
      +nb_participants(Id,.length(LP)); // annotazione 
      .send(LP,tell,cfp(Id,Task));
      // the deadline of the CNP is now + 4 seconds (or all proposals were received)
      .wait(all_proposals_received(Id), 4000, _); // aspetta  secondi (tempo di risposta)
      !contract(Id). // parte il goal del contratto)

// this plan needs to be atomic so as not to accept
// proposals or refusals while contracting
@lc1[atomic]
+!contract(CNPId)
   :  cnp_state(CNPId,propose)
   <- -cnp_state(CNPId,_);
      +cnp_state(CNPId,contract); // passaggio di fase 
      .findall(offer(O,A),propose(CNPId,O)[source(A)],L); // trova tutte le offerte --> collezionando tutte le proposte arrivate dalla sorgente
      .print("Offers are ",L); // stampa delle offerte 
      L \== []; // constraint the plan execution to at least one offer // se non sono vuote
      .min(L,offer(WOf,WAg)); // sort offers, the first is the best // ordina e la prima offerta Ã¨ la migliore
      .print("Winner is ",WAg," with ",WOf); // stampa chi ha fatto l'offerta migliore
      !announce_result(CNPId,L,WAg);
      -+cnp_state(CNPId,finished).

// nothing todo, the current phase is not 'propose'
@lc2 +!contract(_).

-!contract(CNPId) // refuse
   <- .print("CNP ",CNPId," has failed!").

+!announce_result(_,[],_).
// announce to the winner // invio al winner ACCEPT PROPOSAL
+!announce_result(CNPId,[offer(_,WAg)|T],WAg)
   <- .send(WAg,tell,accept_proposal(CNPId));
      !announce_result(CNPId,T,WAg).
// announce to others // invio agli altri // REJECT PROPOSAL
+!announce_result(CNPId,[offer(_,LAg)|T],WAg)
   <- .send(LAg,tell,reject_proposal(CNPId));
      !announce_result(CNPId,T,WAg).
